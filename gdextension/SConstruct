#!/usr/bin/env python
import os
import sys

env = SConscript("godot-cpp/SConstruct")

# For the reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

# The module name
module_name = "godui"

# The folder of the extension binaries
bin_folder = "../project/addons/{}/bin".format(module_name)

# Root directory of the extension code
root_folder = "src/"

folders = [x[0] for x in os.walk(root_folder)]
sources = None
for folder in folders:
    fg = Glob(os.path.join(folder, '*.cpp'))
    if sources == None:
        sources = fg
    else:
        sources = sources + fg

env.Append(CPPPATH=folders)

if env["platform"] == "macos":
    library = env.SharedLibrary(
        "{}/lib{}.{}.{}.framework/lib{}.{}.{}".format(
            bin_folder,
            module_name, env["platform"], env["target"], 
            module_name, env["platform"], env["target"]
        ),
        source=sources,
    )
else:
    library = env.SharedLibrary(
        "{}/lib{}{}{}".format(bin_folder, module_name, env["suffix"], env["SHLIBSUFFIX"]),
        source=sources,
    )

Default(library)
